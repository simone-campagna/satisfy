#!/usr/bin/env satisfy
<|Solve the following cryptarithm:
<|   ab * c == dbe
<|    +   -      -
<|  fcb / b ==  ag
<|  ______________
<|
<|  dda + g == ddh
<|
<|where a, b, c, d, e, f, g, h are different digits.
<|

# Define problem variables with their domain:
a, b, c, d, e, f, g, h :: [0:9]

# Define useful macros:
ab := 10*a + b
dbe := 100*d + 10*b + e
fcb := 100*f + 10*c + b
ag := 10*a + g
dda := 100*d + 10*d + a
ddh := 100*d + 10*d + h

# Solution output:
!|=== solution [{_stats.count:2d}]:
!|    {ab} * {c} == {dbe}
!|     +   -      -
!|   {fcb} / {b} ==  {ag}
!|   ______________
!|
!|   {dda} + {g} == {ddh}
!|

# constraints:
all_different(a, b, c, d, e, f, g, h)

ab * c == dbe
fcb / b ==  ag
dda + g == ddh

ab + fcb == dda
c - b == g
dbe - ag == ddh
