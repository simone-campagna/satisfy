#!/usr/bin/env satisfy

[begin] [[[
Solve the following cryptarithm:

   AB * C == DBE
    +   -      -
  FCB / B ==  AG
  ______________

  DDA + G == DDH

where A, B, C, D, E, F, G, H are different digits.

]]]

# Define problem variables with their domain:
A, B, C, D, E, F, G, H :: [0:9]

# Define useful macros:
AB := 10*A + B
DBE := 100*D + 10*B + E
FCB := 100*F + 10*C + B
AG := 10*A + G
DDA := 100*D + 10*D + A
DDH := 100*D + 10*D + H

# Solution output:
[solution] [[[
=== solution [{_COUNT:2d}]:

    {AB} * {C} == {DBE}
     +   -      -
   {FCB} / {B} ==  {AG}
   ______________

   {DDA} + {G} == {DDH}

]]]

# constraints:
all_different(A, B, C, D, E, F, G, H)

# horizontal:
AB * C == DBE
FCB / B ==  AG
DDA + G == DDH

# vertical:
AB + FCB == DDA
C - B == G
DBE - AG == DDH
