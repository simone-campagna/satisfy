#!/usr/bin/env satisfy

# Print a description of the problem:
[begin] [[[
A rectangular garden is to be constructed using a rock wall as one side of the garden,
and wire fencing for the other three sides.
        y
   +-----------+
   |           |
   |           | x
   |           |
   #############
       rock 

Given N meters of wire fencing, determine the dimensions that would create a garden of
maximum area. What is the maximum area?

]]]

# Read input parameter (fence length)
N := [input] "How many meters of fence? "

# Print a line for each found solution:
[solution] "solution[{_COUNT:2d}]: x={x} y={y} area={area} [elapsed: {_ELAPSED:.2f}s]"

# Print the optimal solution:
[optimal-solution] [[[
=== {_OPTIMAL} solution:

        {y:^3d}
   +-----------+
   |           |
   |           | {x:^3d} [area: {area}]
   |           |
   #############
       rock 

]]]

# Print something at the end:
[end] "All done! [solutions: {_COUNT}, elapsed: {_ELAPSED:.2f}s]"

### DOMAIN definition:
D = [1:N]

### VARIABLES definition:
x, y :: D

### MACROS definition:
area := x * y
fence_length := 2 * x + y

### CONSTRAINTS:
fence_length == N

### OPTIMIZATION:
maximize(area)
